#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Процедура обновления доступности элементов формы при ее открытии.
//
// Параметры:
//  Отказ - Булево - Признак отказа от открытия формы. Если в процедуре присвоить данному параметру значение Истина,
//                 то форма не будет открыта.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьДоступностьЭлементов();
КонецПроцедуры

// Процедура, вызываемая при вставке данных из буфера обмена.
//
// Параметры:
//  Значение - Произвольный - Значение, вставляемое из буфера обмена.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки события.
//
// Описание:
//  Процедура вызывает процедуру РаботаСБуфером, которая обрабатывает данные из буфера обмена.
//  Стандартная обработка события не требуется,
// поэтому параметр СтандартнаяОбработка устанавливается в Ложь внутри процедуры РаботаСБуфером.
&НаКлиенте
Процедура ПриВставкеИзБуфераОбмена(Значение, СтандартнаяОбработка)
	
	РаботаСБуфером();
	
КонецПроцедуры

// Процедура проверяет наличие полных прав у пользователя и устанавливает отбор по департаменту "Складской департамент"
// для элемента формы ТЗДанныеФизЛицо, если полных прав нет.
//
// Параметры:
//  Отказ - Булево - Признак отказа от создания формы.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной обработки события создания формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолныеПрава = РольДоступна("ПолныеПрава");
	
	Если Не ПолныеПрава Тогда
		Департамент = Справочники.ПЛ_Департамент_ЗУП.НайтиПоНаименованию("Складской департамент", Истина);
		ПараметрыВыбора = Новый ФиксированныйМассив(Новый ПараметрВыбора("Отбор.ПЛ_Департамент_ЗУП", Департамент));
		Элементы.ТЗДанныеФизЛицо.ПараметрыВыбора = ПараметрыВыбора;
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает выбор физического лица в поле "ФизЛицоПриемник".
//
// Параметры:
//  Элемент - ПолеФормы - Поле формы, в котором был сделан выбор.
//  ВыбранноеЗначение - СправочникСсылка.ФизическиеЛица - Выбранное физическое лицо.
//  ДополнительныеДанные - Произвольный - Дополнительные данные выбора.
//  СтандартнаяОбработка - Булево - Признак стандартной обработки события.
//
&НаКлиенте
Процедура ФизЛицоПриемникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательПриемник = ПользовательПоФизическомуЛицу(ВыбранноеЗначение);
	
	Если Не ЗначениеЗаполнено(ПользовательПриемник) Тогда
		Возврат;
	КонецЕсли;
	
	АутентификацияПользователя = АутентификацияПользователяНаСервере(ПользовательПриемник);
	
	ПользовательИБАутентификацияСтандартнаяПриемник = АутентификацияПользователя.АутентификацияСтандартная;
	ПользовательИБАутентификацияОСПриемник = АутентификацияПользователя.АутентификацияОС;
	ПользовательИБПользовательОСПриемник = АутентификацияПользователя.ПользовательОС;
	
	ОчиститьЗаполнитьТЧ("ГруппыПользователейПриемника", ПользовательПриемник, "ГруппыПользователей");
	ОчиститьЗаполнитьТЧ("ГруппыДоступаПриемника", ПользовательПриемник, "ГруппыДоступа");
	
	Попытка
		ПотребоватьСменуПароляПриВходеПриемник = ПотребоватьСменуПароляПриВходе(ПользовательПриемник);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ДоступСкладыТСД(ПользовательПриемник, ДоступВТСДПриемник, СкладыТСДПриемник);
	
	ОбновитьДоступностьЭлементов();
	
КонецПроцедуры

// Обработчик события "НачалоВыбора" элемента формы "ПользовательИБПользовательОСИсточник".
// Открывает форму выбора пользователя операционной системы для заполнения реквизита.
//
// Параметры:
//  Элемент - ПолеФормы - элемент формы, для которого произошло событие.
//  ДанныеВыбора - СписокЗначений - список значений для выбора.
//  ВыборДобавлением - Булево - признак выбора добавлением.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной обработки события.
//
&НаКлиенте
Процедура ПользовательИБПользовательОСИсточникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением,
		СтандартнаяОбработка)
	#Если НЕ ВебКлиент И НЕ МобильныйКлиент Тогда
		ОткрытьФорму("Справочник.Пользователи.Форма.ВыборПользователяОперационнойСистемы", , Элемент);
	#КонецЕсли
КонецПроцедуры

// Открывает форму выбора пользователя операционной системы для элемента формы "ПользовательИБПользовательОСПриемник".
//
// Параметры:
//  Элемент - ПолеФормы - Элемент формы, для которого необходимо открыть форму выбора.
//  ДанныеВыбора - СписокЗначений - Список значений для выбора.
//  ВыборДобавлением - Булево - Признак выбора добавлением.
//  СтандартнаяОбработка - Булево - Признак стандартной обработки события.
//
&НаКлиенте
Процедура ПользовательИБПользовательОСПриемникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением,
		СтандартнаяОбработка)
	#Если НЕ ВебКлиент И НЕ МобильныйКлиент Тогда
		ОткрытьФорму("Справочник.Пользователи.Форма.ВыборПользователяОперационнойСистемы", , Элемент);
	#КонецЕсли
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы "ПользовательИБАутентификацияОСПриемник".
//
// Параметры:
//  Элемент - ПолеФормы - Элемент формы, для которого произошло событие.
//
// Описание:
//  Обновляет доступность элементов формы в зависимости от значения элемента "ПользовательИБАутентификацияОСПриемник".
&НаКлиенте
Процедура ПользовательИБАутентификацияОСПриемникПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТЗДанные

// Процедура обработки выбора значения в элементе таблицы ТЗДанные.
//
// Параметры:
//  Элемент - ПолеФормы - элемент таблицы ТЗДанные, в котором было выбрано значение.
//  ВыбранноеЗначение - <Тип значения, выбранного в элементе таблицы> - выбранное значение.
//  ДополнительныеДанные - Произвольный - дополнительные данные, переданные в событие.
//  СтандартнаяОбработка - Булево - флаг стандартной обработки события.
//
&НаКлиенте
Процедура ТЗДанныеФизЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Пользователь = ТЗДанныеФизЛицоОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		Элементы.ТЗДанные.ТекущиеДанные.Пользователь = Пользователь;
		Элементы.ТЗДанные.ТекущиеДанные.Авторизация = СвойстваПользователяИБНаСервере(Пользователь);
	ИначеЕсли Пользователь = Неопределено Тогда
		ЭтотОбъект.ТЗДанные.Удалить(Элементы.ТЗДанные.ТекущиеДанные);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Произошла нештатная ситуация");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СвойстваПользователяИБНаСервере(Знач Пользователь)
	НаименованиеПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "Наименование");
	Возврат Пользователи.СвойстваПользователяИБ(НаименованиеПользователя).АутентификацияСтандартная;
КонецФункции

// Функция ищет пользователя по выбранному физическому лицу с учетом прав доступа
// и принадлежности сотрудника к определенному департаменту и должности.
//
// Параметры:
//  ВыбранноеЗначение - СправочникСсылка.ФизическиеЛица - Физическое лицо, для которого ищется пользователь.
//
// Возвращаемое значение:
//  СправочникСсылка.Пользователи - Найденный пользователь или Неопределено,
// если пользователь не найден или не пройдена проверка прав доступа.
//
&НаСервере
Функция ТЗДанныеФизЛицоОбработкаВыбораНаСервере(Знач ВыбранноеЗначение)
	
	Если Не ПолныеПрава Тогда
		
		РекСотрудника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбранноеЗначение,
				"ПЛ_Департамент_ЗУП, ПЛ_Должность");
		
		Если ПроверкаДепартаментаИДолжности(РекСотрудника) Тогда
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПользовательПоФизическомуЛицу(ВыбранноеЗначение);
	
КонецФункции

// Проверяет соответствие департамента и должности сотрудника заданным значениям.
//
// Параметры:
//  РекСотрудника - Структура - Реквизиты сотрудника, содержащие поля ПЛ_Департамент_ЗУП и ПЛ_Должность.
//
// Возвращаемое значение:
//  Булево - Истина, если департамент сотрудника равен текущему департаменту
// и должность сотрудника является одной из: Кладовщик, Контролер или Приемщик.
//
&НаСервере
Функция ПроверкаДепартаментаИДолжности(Знач РекСотрудника)
	Возврат Департамент = РекСотрудника.ПЛ_Департамент_ЗУП
		И (РекСотрудника.ПЛ_Должность = Справочники.ПЛ_Должности.Кладовщик
			Или РекСотрудника.ПЛ_Должность = Справочники.ПЛ_Должности.Контролер
			Или РекСотрудника.ПЛ_Должность = Справочники.ПЛ_Должности.Приемщик);
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

// Асинхронно вызывает процедуру РаботаСБуфером для вставки данных из буфера обмена.
//
// Параметры:
//  Команда - КомандаФормы - Команда, вызвавшая процедуру.
//
&НаКлиенте
Процедура ВставитьИзБуфера(Команда)
	
	РаботаСБуфером();
	
КонецПроцедуры

// Проверяет доступность буфера обмена, извлекает данные из него, разделяет их на строки и заполняет таблицу значений.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Асинх Процедура РаботаСБуфером()
	
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ПредупреждениеАсинх("Использование буфера обмена не доступно!");
	КонецЕсли;
	
	ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Текст;
	
	Если Ждать СредстваБуфераОбмена.СодержитДанныеАсинх(ФорматДанных) Тогда
		
		РезультатБуфера = Ждать СредстваБуфераОбмена.ПолучитьДанныеАсинх(ФорматДанных);
		
		МассивСтрок = СтрРазделить(РезультатБуфера, Символы.ПС);
		
		ЗаполнитьТаблицуЗначений(МассивСтрок);
		
		ПересчитатьНомераСтрокТЗ();
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу значений на основе массива строк и данных из справочников ФизическиеЛица и Пользователи.
//
// Параметры:
//  МассивСтрок - Массив - массив строк,
// содержащий ФИО и доступы пользователей в формате "ФИО<TAB>Доступ1С<TAB>ДоступТСД".
//
&НаСервере
Процедура ЗаполнитьТаблицуЗначений(Знач МассивСтрок)
	
	ТаблицаПользователей = Новый ТаблицаЗначений;
	
	ТаблицаПользователей.Колонки.Добавить("ФИО");
	ТаблицаПользователей.Колонки.Добавить("Доступ1С");
	ТаблицаПользователей.Колонки.Добавить("ДоступТСД");
	
	Для Каждого Строка Из МассивСтрок Цикл
		
		ФИОИДоступы = СтрРазделить(Строка, Символы.Таб);
		
		Если ФИОИДоступы.Количество() = 3 Тогда
			
			НоваяСтрока = ТаблицаПользователей.Добавить();
			
			НоваяСтрока.ФИО = ФИОИДоступы[0];
			НоваяСтрока.Доступ1С = ФИОИДоступы[1];
			НоваяСтрока.ДоступТСД = СокрЛП(ФИОИДоступы[2]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ФизическиеЛица.Ссылка, НЕОПРЕДЕЛЕНО) КАК ФизЛицо
		|ПОМЕСТИТЬ ВТ_ФизЛица
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Наименование В(&Наименование)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Пользователи.Ссылка, НЕОПРЕДЕЛЕНО) КАК Пользователь,
		|	ВТ_ФизЛица.ФизЛицо КАК ФизЛицо,
		|	ВТ_ФизЛица.ФизЛицо.Наименование КАК ФизЛицоНаименование
		|ИЗ
		|	ВТ_ФизЛица КАК ВТ_ФизЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ВТ_ФизЛица.ФизЛицо = Пользователи.ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("Наименование", ТаблицаПользователей.ВыгрузитьКолонку("ФИО"));
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаЗапроса Из РезультатЗапроса Цикл
		
		НоваяСтрока = ТЗДанные.Добавить();
		
		НоваяСтрока.ФизЛицо = СтрокаЗапроса.ФизЛицо;
		НоваяСтрока.Пользователь = СтрокаЗапроса.Пользователь;
		НоваяСтрока.Склады = СкладыДляЗагрузки;
		
		НайденнаяСтрока = ТаблицаПользователей.Найти(СтрокаЗапроса.ФизЛицоНаименование);
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			НоваяСтрока.ДоступВ1С = НайденнаяСтрока.Доступ1С;
			НоваяСтрока.ДоступВТСД = НайденнаяСтрока.ДоступТСД;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура переносит настройки пользователя из источника в приемник.
//
// Параметры:
//  Команда - <Тип не определен> - Не используется в процедуре.
//
// Описание:
//  Процедура копирует настройки аутентификации, группы пользователей и группы доступа из источника в приемник.
//  Если флаг ОчищатьНастройкиПриемника установлен, то настройки приемника будут очищены перед копированием.
//  Дополнительно копируются настройки доступа в ТСД и склады ТСД.
//  После копирования настроек вызывается процедура ОбновитьДоступностьЭлементов.
&НаКлиенте
Процедура СкопироватьНастройки(Команда)
	
	ПользовательИБАутентификацияСтандартнаяПриемник = ПользовательИБАутентификацияСтандартнаяИсточник;
	ПользовательИБАутентификацияОСПриемник = ПользовательИБАутентификацияОСИсточник;
	
	Если ОчищатьНастройкиПриемника Тогда
		ГруппыПользователейПриемника.Очистить();
		ГруппыДоступаПриемника.Очистить();
	КонецЕсли;
	
	СкопироватьГруппыПользователей();
	СкопироватьГруппыДоступа();
	СкопироватьСкладыТСД();
	
	ОбновитьДоступностьЭлементов();
	
КонецПроцедуры

Процедура СкопироватьГруппыПользователей()
	Для Каждого СтрокаТЗ Из ГруппыПользователейИсточника Цикл
		ОтборПоиска = Новый Структура("ГруппаПользователей", СтрокаТЗ.ГруппаПользователей);
		НайденныеСтроки = ГруппыПользователейПриемника.НайтиСтроки(ОтборПоиска);
		
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			НоваяСтрока = ГруппыПользователейПриемника.Добавить();
			НоваяСтрока.ГруппаПользователей = СтрокаТЗ.ГруппаПользователей;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СкопироватьГруппыДоступа()
	Для Каждого СтрокаТЗ Из ГруппыДоступаИсточника Цикл
		ОтборПоиска = Новый Структура("ГруппаДоступа", СтрокаТЗ.ГруппаДоступа);
		НайденныеСтроки = ГруппыДоступаПриемника.НайтиСтроки(ОтборПоиска);
		
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Если НеПереноситьИнциденты Тогда
				ПоискВНаименовании = СтрНайти(СтрокаТЗ.ГруппаДоступа.Наименование, "инцидент");
				Если ЗначениеЗаполнено(ПоискВНаименовании) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НоваяСтрока = ГруппыДоступаПриемника.Добавить();
			НоваяСтрока.ГруппаДоступа = СтрокаТЗ.ГруппаДоступа;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СкопироватьСкладыТСД()
	
	ДоступВТСДПриемник = ДоступВТСДИсточник;
	
	Если ОчищатьНастройкиПриемника Тогда
		СкладыТСДПриемник = СкладыТСДИсточник.Скопировать();
	Иначе
		Для Каждого СтрСкладыТСД Из СкладыТСДИсточник Цикл
			РезультатПоиска = СкладыТСДПриемник.НайтиПоЗначению(СтрСкладыТСД.Значение);
			Если РезультатПоиска = Неопределено Тогда
				СкладыТСДПриемник.Добавить(СтрСкладыТСД.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Процедура переносит настройки пользователей с источника на приемник.
//
// Параметры:
//  ПользовательИБАутентификацияСтандартнаяИсточник
// - СправочникСсылка.Пользователи - Пользователь источника с аутентификацией стандартной.
//  ПользовательИБАутентификацияОСИсточник
// - СправочникСсылка.Пользователи - Пользователь источника с аутентификацией ОС.
//  ГруппыПользователейИсточника - ТаблицаЗначений - Таблица групп пользователей источника.
//  ГруппыПользователейПриемника - ТаблицаЗначений - Таблица групп пользователей приемника.
//  Объект - УправляемаяФорма - Форма объекта, содержащая таблицу ГруппыПользователейПриемника.
//
&НаСервере
Процедура ПеренестиНастройкиНаСервере()
	
	ПользовательИБАутентификацияСтандартнаяПриемник = ПользовательИБАутентификацияСтандартнаяИсточник;
	ПользовательИБАутентификацияОСПриемник = ПользовательИБАутентификацияОСИсточник;
	
	Для Каждого СтрокаТЗ Из ГруппыПользователейИсточника Цикл
		
		ОтборПоиска = Новый Структура("ГруппаПользователей", СтрокаТЗ.ГруппаПользователей);
		НайденныеСтроки = Объект.ГруппыПользователейПриемника.НайтиСтроки(СтрокаТЗ);
		
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			
			НоваяСтрока = ГруппыПользователейПриемника.Добавить();
			НоваяСтрока.ГруппаПользователей = СтрокаТЗ.ГруппаПользователей;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Копирует настройки на сервере.
//
// Параметры:
//  Команда - КомандаФормы - Не используется в процедуре, требуется для соответствия сигнатуре обработчика команды.
//
&НаКлиенте
Процедура УстановитьНастройки(Команда)
	УстановитьНастройкиНаСервере();
КонецПроцедуры

// Процедура копирования настроек пользователя на сервере.
//
// Параметры:
//  ФизЛицоПриемник - СправочникСсылка.ФизическиеЛица - Физическое лицо приемник настроек.
//  ПользовательИБАутентификацияОСПриемник - Булево - Признак аутентификации пользователя по ОС.
//  ПользовательИБПользовательОСПриемник - СправочникСсылка.Пользователи - Пользователь ОС.
//  ПользовательИБАутентификацияСтандартнаяПриемник - Булево - Признак стандартной аутентификации пользователя.
//  ПотребоватьСменуПароляПриВходеПриемник - Булево - Признак необходимости смены пароля при входе.
//  УдалятьИзГрупп - Булево - Признак необходимости удаления пользователя из групп.
//  ГруппыПользователейПриемника - Массив - Массив групп пользователей приемника.
//  ГруппыДоступаПриемника - Массив - Массив групп доступа приемника.
//  ДоступВТСДПриемник - Булево - Признак доступа в ТСД.
//  СкладыТСДПриемник - Массив - Массив складов ТСД приемника.
//  НомерЗадачиБитрикс - Строка - Номер задачи в Битрикс.
//  ДелегироватьЗадачу - Булево - Признак необходимости делегирования задачи.
//
&НаСервере
Процедура УстановитьНастройкиНаСервере()
	
	ПрерватьОбработку = Ложь;
	
	Если Не ЗначениеЗаполнено(ФизЛицоПриемник) Тогда
		
		ОбщегоНазначения.СообщитьПользователю("Не заполнено физическое лицо", , "ФизЛицоПриемник");
		ПрерватьОбработку = Истина;
		
	КонецЕсли;
	Если ПользовательИБАутентификацияОСПриемник И Не ЗначениеЗаполнено(ПользовательИБПользовательОСПриемник) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не заполнен пользователь", , "ПользовательИБПользовательОСПриемник");
		ПрерватьОбработку = Истина;
	КонецЕсли;
	
	Если ПрерватьОбработку Тогда
		Возврат;
	КонецЕсли;
	
	ЗадаватьПароль = Ложь;
	
	УстановитьОсновныеНастройки(ЗадаватьПароль);
	ДобавитьПользователяВГруппы();
	ОбработатьНастройкиТСД();
	ОтправитьКомментарийВБитриксНастройки(ЗадаватьПароль);
	
КонецПроцедуры

Процедура УстановитьОсновныеНастройки(ЗадаватьПароль)
	СоответствиеПользователя1С = Новый Соответствие;
	СоответствиеПользователя1С.Вставить("ФизЛицо", ФизЛицоПриемник);
	СоответствиеПользователя1С.Вставить("Пользователь", ПользовательПриемник);
	СоответствиеПользователя1С.Вставить("ВременныйПароль", "");
	
	ЗадаватьПароль = ПользовательИБАутентификацияСтандартнаяПриемник И ПотребоватьСменуПароляПриВходеПриемник;
	
	СвойстваПароля = СвойстваПароля();
	
	ДобавитьОбновитьПользователя1С(СоответствиеПользователя1С, ЗадаватьПароль, СвойстваПароля,
			ПользовательИБАутентификацияОСПриемник, ПользовательИБПользовательОСПриемник,
			ПользовательИБАутентификацияСтандартнаяПриемник);
	
	ПользовательПриемник = СоответствиеПользователя1С["Пользователь"];
	ВременныйПарольПриемник = СоответствиеПользователя1С["ВременныйПароль"];
	
	Если ПотребоватьСменуПароляПриВходеПриемник Тогда
		// Создаем запись в регистре сведений, чтобы пользователь
		// обязан был сменить пароль при входе в 1С.
		НаборЗаписей = РегистрыСведений.СведенияОПользователях.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ПользовательПриемник);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			СведенияОПользователе = НаборЗаписей.Добавить();
			СведенияОПользователе.Пользователь = ПользовательПриемник;
		Иначе
			СведенияОПользователе = НаборЗаписей[0];
		КонецЕсли;
		СведенияОПользователе.ПотребоватьСменуПароляПриВходе = Истина;
		НаборЗаписей.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьПользователяВГруппы()
	СтруктураДляПоиска = Новый Структура("Пользователь", ПользовательПриемник);
	
	// Перед добавлением пользователя в группы сначала удаляем его
	// со всех групп
	Если УдалятьИзГрупп Тогда
		УдалитьПользователяИзГрупп(СтруктураДляПоиска);
	КонецЕсли;
	
	// Добавление пользователя в группы
	ДобавитьПользователяВГруппу(СтруктураДляПоиска);
	
КонецПроцедуры

Процедура УдалитьПользователяИзГрупп(СтруктураДляПоиска)
	
	Для Каждого ГруппаПользователей Из ГруппыДоступаПользователя(ПользовательПриемник, "ГруппыПользователей") Цикл
		
		ГруппаПользователейОбъект = ГруппаПользователей.ГруппаПользователей.ПолучитьОбъект();
		
		НайденныеЗначения = ГруппаПользователейОбъект.Состав.НайтиСтроки(СтруктураДляПоиска);
		
		Если ЗначениеЗаполнено(НайденныеЗначения) Тогда
			
			ГруппаПользователейОбъект.Состав.Удалить(ГруппаПользователейОбъект.Состав.Индекс(НайденныеЗначения[0]));
			ГруппаПользователейОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ГруппаДоступа Из ГруппыДоступаПользователя(ПользовательПриемник, "ГруппыПользователей") Цикл
		
		ГруппаДоступаОбъект = ГруппаДоступа.ГруппаДоступа.ПолучитьОбъект();
		
		НайденныеЗначения = ГруппаДоступаОбъект.Пользователи.НайтиСтроки(СтруктураДляПоиска);
		
		Если ЗначениеЗаполнено(НайденныеЗначения) Тогда
			
			ГруппаДоступаОбъект.Пользователи.Удалить(ГруппаДоступаОбъект.Пользователи.Индекс(НайденныеЗначения[0]));
			ГруппаДоступаОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьПользователяВГруппу(СтруктураДляПоиска)
	
	Для Каждого ГруппаПользователей Из ГруппыПользователейПриемника Цикл
		
		ГруппаПользователейОбъект = ГруппаПользователей.ГруппаПользователей.ПолучитьОбъект();
		
		// Проверка наличия повторяющихся значений.
		НайденныеЗначения = ГруппаПользователейОбъект.Состав.НайтиСтроки(СтруктураДляПоиска);
		
		Если Не ЗначениеЗаполнено(НайденныеЗначения) Тогда
			
			НоваяСтрокаГруппаПользователей = ГруппаПользователейОбъект.Состав.Добавить();
			НоваяСтрокаГруппаПользователей.Пользователь = ПользовательПриемник;
			ГруппаПользователейОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ГруппаДоступа Из ГруппыДоступаПриемника Цикл
		
		ГруппаДоступаОбъект = ГруппаДоступа.ГруппаДоступа.ПолучитьОбъект();
		
		// Проверка наличия повторяющихся значений.
		НайденныеЗначения = ГруппаДоступаОбъект.Пользователи.НайтиСтроки(СтруктураДляПоиска);
		
		Если Не ЗначениеЗаполнено(НайденныеЗначения) Тогда
			
			НоваяСтрокаГруппаДоступа = ГруппаДоступаОбъект.Пользователи.Добавить();
			НоваяСтрокаГруппаДоступа.Пользователь = ПользовательПриемник;
			ГруппаДоступаОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

Процедура ОбработатьНастройкиТСД()
	Если КопироватьНастройки Тогда
		СкопироватьВсеНастройки(ПользовательИсточник, ПользовательПриемник);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоступВТСДПриемник) И ЗначениеЗаполнено(СкладыТСДПриемник) Тогда
		СтруктураПользовательСклады = Новый Структура("Пользователь, Склады, ДоступВТСД",
				ПользовательПриемник, СкладыТСДПриемник, ДоступВТСДПриемник);
		ДобавитьОбновитьПользователяТСД(СтруктураПользовательСклады);
	КонецЕсли;
КонецПроцедуры

Процедура ОтправитьКомментарийВБитриксНастройки(Знач ЗадаватьПароль)
	Если ЗначениеЗаполнено(НомерЗадачиБитрикс) Тогда
		НомерЗадачи = СокрЛП(НомерЗадачиБитрикс);
		КомментарийДляЗадачи = "Готово.";
		
		Если ЗадаватьПароль И ЗначениеЗаполнено(ВременныйПарольПриемник) Тогда
			КомментарийДляЗадачи = КомментарийДляЗадачи + " Временный пароль:" + Символы.ПС
				+ ПользовательПриемник + " - " + ВременныйПарольПриемник;
		Иначе
			КомментарийДляЗадачи = КомментарийДляЗадачи + Символы.ПС +
				ПользовательПриемник;
		КонецЕсли;
		
		Если ПользовательИБАутентификацияОСПриемник И ЗначениеЗаполнено(ПользовательИБПользовательОСПриемник) Тогда
			КомментарийДляЗадачи = КомментарийДляЗадачи + Символы.ПС + "Вход доменный.";
		КонецЕсли;
		
		// 17029 Идентификатор 1С пользователя Битрикс
		ПЛ_Битрикс24.ОтправитьКомментарийВЗадачу(НомерЗадачи, КомментарийДляЗадачи, "17029");
		
		Если ДелегироватьЗадачу Тогда
			Результат = ПолучитьИДПостановщикаЗадачи(НомерЗадачи);
			Ответ1С = ПЛ_МодульВебСервисов.ПреобразоватьИЗJSON(Результат.ПолучитьТелоКакСтроку(),,Истина);
			ИДПоставновщика = Ответ1С.Получить("result").Получить("task").Получить("createdBy");
			ДелегироватьЗадачу(НомерЗадачи, ИДПоставновщика);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает пользователя по физическому лицу.
//
// Параметры:
//  ФизЛицо - СправочникСсылка.ФизическиеЛица - Физическое лицо, для которого необходимо получить пользователя.
//
// Возвращаемое значение:
//  СправочникСсылка.Пользователи - Найденный пользователь или пустая ссылка, если пользователь не найден.
//
&НаСервереБезКонтекста
Функция ПользовательПоФизическомуЛицу(Знач ФизЛицо)
	
	Результат = Справочники.Пользователи.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пользователи.Ссылка КАК Пользователь
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.ФизическоеЛицо = &ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Результат = Выборка.Пользователь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает структуру с параметрами для отправки данных о пользователе в ТСД.
//
// Возвращаемое значение:
//  Структура - Параметры пользователя, содержащая следующие ключи:
//    * id - Строка - Идентификатор пользователя.
//    * name - Строка - Имя пользователя.
//    * password - Строка - Пароль пользователя.
//    * description - Строка - Описание пользователя.
//    * barcode - Строка - Штрихкод пользователя.
//    * groupId - Строка - Идентификатор группы пользователя.
//    * groupName - Строка - Наименование группы пользователя.
//    * warehouseIds - Массив - Идентификаторы складов, к которым имеет доступ пользователь.
&НаСервереБезКонтекста
Функция ПараметрыОтправкиПользователея()
	
	Результат = Новый Структура;
	Результат.Вставить("id", "");
	Результат.Вставить("name", "");
	Результат.Вставить("password", "");
	Результат.Вставить("description", "");
	Результат.Вставить("barcode", "");
	Результат.Вставить("groupId", "");
	Результат.Вставить("groupName", "");
	Результат.Вставить("warehouseIds", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Процедура для выполнения команды "Завести пользователей".
//
// Параметры:
//  Команда - <Тип команды> - Команда, которая вызвала процедуру.
//
// Процедура проверяет заполнение данных и вызывает процедуру ЗавестиПользователейНаСервере.
&НаКлиенте
Процедура ЗавестиПользователей(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ЗавестиПользователейНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура создает или обновляет пользователей в системе,
// добавляет их в группы доступа и отправляет комментарии в задачи Битрикс24.
//
// Параметры:
//  ТЗДанные - ТаблицаЗначений - Таблица с данными пользователей, содержащая колонки:
//    * Пользователь - СправочникСсылка.Пользователи - Ссылка на пользователя.
//    * ФизЛицо - СправочникСсылка.ФизическиеЛица - Ссылка на физическое лицо.
//    * ДоступВ1С - Строка - Тип доступа в 1С ("Контроль" или "Приемка").
//    * ДоступВТСД - Строка - Тип доступа в ТСД.
//    * ЗадачаБитрикс - Строка - Номер задачи в Битрикс24.
//    * Делегировать - Булево - Признак делегирования задачи.
//
//
&НаСервере
Процедура ЗавестиПользователейНаСервере()
	
	ГДКонтроль = Справочники.ГруппыПользователей.НайтиПоНаименованию("Сотрудник склада", Истина).ПолучитьОбъект();
	ГДПриемка = Справочники.ГруппыПользователей.НайтиПоНаименованию("Сотрудник приемки", Истина).ПолучитьОбъект();
	
	СвойстваПароля = СвойстваПароля();
	
	Для Каждого СтрокаПользователя Из ТЗДанные Цикл
		ОбработатьПользователя(СтрокаПользователя, ГДКонтроль, ГДПриемка, СвойстваПароля);
	КонецЦикла;
	
	ГДКонтроль.Записать();
	ГДПриемка.Записать();
	
КонецПроцедуры

Процедура ОбработатьПользователя(СтрокаПользователя, ГДКонтроль, ГДПриемка, СвойстваПароля)
	
	СтрокаПриемка = "Приемка";
	СтрокаПользователь = "Пользователь";
	
	СтруктураДляПоиска = Новый Структура(СтрокаПользователь, СтрокаПользователя.Пользователь);
	ЗадаватьПароль = (СтрокаПользователя.ДоступВ1С = "Контроль" Или СтрокаПользователя.ДоступВ1С = СтрокаПриемка)
		И Не СтрокаПользователя.Авторизация;
	
	СоответствиеПользователя1С = Новый Соответствие;
	СоответствиеПользователя1С.Вставить("ФизЛицо", СтрокаПользователя.ФизЛицо);
	СоответствиеПользователя1С.Вставить(СтрокаПользователь, СтрокаПользователя.Пользователь);
	СоответствиеПользователя1С.Вставить("ВременныйПароль", "");
	
	АвторизацияСтандартная = СтрокаПользователя.Авторизация Или ЗадаватьПароль;
	
	ДобавитьОбновитьПользователя1С(СоответствиеПользователя1С, ЗадаватьПароль, СвойстваПароля, , ,
		АвторизацияСтандартная);
	
	СтрокаПользователя.Пользователь = СоответствиеПользователя1С[СтрокаПользователь];
	СтрокаПользователя.ВременныйПароль = СоответствиеПользователя1С["ВременныйПароль"];
	
	Если ЗадаватьПароль Тогда
		ПотребоватьСменуПароля(СтрокаПользователя);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаПользователя.ДоступВТСД) Тогда
		ДобавитьОбновитьПользователяТСД(СтрокаПользователя);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаПользователя.ЗадачаБитрикс) Тогда
		ОтправитьКомментарийВБитрикс(СтрокаПользователя, ЗадаватьПароль);
	КонецЕсли;
	
	// Проверка наличия повторяющихся значений и добавление в группы доступа
	Если СтрокаПользователя.ДоступВ1С = СтрокаПриемка
		Или СтрокаПользователя.ДоступВ1С = "Контроль" Тогда
		Если СтрокаПользователя.ДоступВ1С = СтрокаПриемка Тогда
			ДобавитьВГруппуЕслиОтсутствует(ГДПриемка, СтрокаПользователя.Пользователь);
		КонецЕсли;
		ДобавитьВГруппуЕслиОтсутствует(ГДКонтроль, СтрокаПользователя.Пользователь);
	КонецЕсли;
КонецПроцедуры

//&НаСервере
//Процедура ЗавестиПользователейНаСервере()
//	
//	ГДКонтроль = Справочники.ГруппыПользователей.НайтиПоНаименованию("Сотрудник склада", Истина).ПолучитьОбъект();
//	ГДПриемка = Справочники.ГруппыПользователей.НайтиПоНаименованию("Сотрудник приемки", Истина).ПолучитьОбъект();
//	
//	СвойстваПароля = СвойстваПароля();

//	СтрокаПриемка = "Приемка";
//	СтрокаПользователь = "Пользователь";
//	
//	Для Каждого СтрокаПользователя Из ТЗДанные Цикл
//		
//		СтруктураДляПоиска = Новый Структура(СтрокаПользователь, СтрокаПользователя.Пользователь);
//		ЗадаватьПароль = (СтрокаПользователя.ДоступВ1С = "Контроль" Или СтрокаПользователя.ДоступВ1С = СтрокаПриемка)
//			И Не СтрокаПользователя.Авторизация;
//		
//		СоответствиеПользователя1С = Новый Соответствие;
//		СоответствиеПользователя1С.Вставить("ФизЛицо", СтрокаПользователя.ФизЛицо);
//		СоответствиеПользователя1С.Вставить(СтрокаПользователь, СтрокаПользователя.Пользователь);
//		СоответствиеПользователя1С.Вставить("ВременныйПароль", "");
//		
//		АвторизацияСтандартная = СтрокаПользователя.Авторизация Или ЗадаватьПароль;
//		
//		ДобавитьОбновитьПользователя1С(СоответствиеПользователя1С, ЗадаватьПароль, СвойстваПароля, , ,
//			АвторизацияСтандартная);
//		
//		СтрокаПользователя.Пользователь = СоответствиеПользователя1С[СтрокаПользователь];
//		СтрокаПользователя.ВременныйПароль = СоответствиеПользователя1С["ВременныйПароль"];
//		
//		Если ЗадаватьПароль Тогда
//			ПотребоватьСменуПароля(СтрокаПользователя);
//		КонецЕсли;
//		
//		Если ЗначениеЗаполнено(СтрокаПользователя.ДоступВТСД) Тогда
//			ДобавитьОбновитьПользователяТСД(СтрокаПользователя);
//		КонецЕсли;
//		
//		Если ЗначениеЗаполнено(СтрокаПользователя.ЗадачаБитрикс) Тогда
//			ОтправитьКомментарийВБитрикс(СтрокаПользователя, ЗадаватьПароль);
//		КонецЕсли;
//		
//		// Проверка наличия повторяющихся значений и добавление в группы доступа
//		Если СтрокаПользователя.ДоступВ1С = СтрокаПриемка
//		Или СтрокаПользователя.ДоступВ1С = "Контроль" Тогда
//			Если СтрокаПользователя.ДоступВ1С = СтрокаПриемка Тогда
//				ДобавитьВГруппуЕслиОтсутствует(ГДПриемка, СтрокаПользователя.Пользователь);
//			КонецЕсли;
//			ДобавитьВГруппуЕслиОтсутствует(ГДКонтроль, СтрокаПользователя.Пользователь);
//		КонецЕсли;
//		
//	КонецЦикла;
//	
//	ГДКонтроль.Записать();
//	ГДПриемка.Записать();
//	
//КонецПроцедуры

Процедура ПотребоватьСменуПароля(СтрокаПользователя)
	
	НаборЗаписей = РегистрыСведений.СведенияОПользователях.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(СтрокаПользователя.Пользователь);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		СведенияОПользователе = НаборЗаписей.Добавить();
		СведенияОПользователе.Пользователь = СтрокаПользователя.Пользователь;
	Иначе
		СведенияОПользователе = НаборЗаписей[0];
	КонецЕсли;
	
	СведенияОПользователе.ПотребоватьСменуПароляПриВходе = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ДобавитьВГруппуЕслиОтсутствует(Группа, Пользователь)
	НайденныеЗначения = Группа.Состав.НайтиСтроки(Новый Структура("Пользователь", Пользователь));
	Если Не ЗначениеЗаполнено(НайденныеЗначения) Тогда
		НоваяСтрока = Группа.Состав.Добавить();
		НоваяСтрока.Пользователь = Пользователь;
	КонецЕсли;
КонецПроцедуры

Процедура ОтправитьКомментарийВБитрикс(СтрокаПользователя, ЗадаватьПароль)
	
	НомерЗадачи = СокрЛП(СтрокаПользователя.ЗадачаБитрикс);
	КомментарийДляЗадачи = "Готово.";
	
	Если ЗадаватьПароль И ЗначениеЗаполнено(СтрокаПользователя.ВременныйПароль) Тогда
		КомментарийДляЗадачи = КомментарийДляЗадачи + " Временный пароль: " + Символы.ПС
			+ СтрокаПользователя.Пользователь + " - " + СтрокаПользователя.ВременныйПароль;
	Иначе
		КомментарийДляЗадачи = КомментарийДляЗадачи + Символы.ПС + СтрокаПользователя.Пользователь;
	КонецЕсли;
	
	ПЛ_Битрикс24.ОтправитьКомментарийВЗадачу(НомерЗадачи, КомментарийДляЗадачи, "17029");
	
	Результат = ПолучитьИДПостановщикаЗадачи(НомерЗадачи);
	Ответ1С = ПЛ_МодульВебСервисов.ПреобразоватьИЗJSON(Результат.ПолучитьТелоКакСтроку(),,Истина);
	ИДПоставновщика = Ответ1С.Получить("result").Получить("task").Получить("createdBy");
	
	Если СтрокаПользователя.Делегировать Тогда
		
		ДелегироватьЗадачу(НомерЗадачи, ИДПоставновщика);
		
	КонецЕсли;
	
КонецПроцедуры

// Функция для получения соединения и заголовков для взаимодействия с Bitrix24.
//
//
// Возвращаемое значение:
//  Структура - содержит два элемента:
//    * Заголовки - Соответствие - заголовки HTTP-запроса, где ключ - имя заголовка, значение - значение заголовка.
//    * Соединение - HTTPСоединение - объект для отправки HTTP-запросов на сервер Bitrix24.
//
&НаСервереБезКонтекста
Функция ПолучитьСоединениеЗаголовкиСBitrix24()
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Content-Charset", "utf-8");
	Сервер = "profitleague.bitrix24.ru/rest/17029/3uedati105hxrgob/";
	
	ssl = Новый ЗащищенноеСоединениеOpenSSL();
	Соединение =  Новый HTTPСоединение(Сервер, , , , , 30, ssl);
	
	Возврат Новый Структура("Заголовки, Соединение", Заголовки, Соединение);
	
КонецФункции

// Делегирует задачу в Bitrix24 другому сотруднику.
//
// Параметры:
//  ИДЗадачи - Строка - Идентификатор задачи в Bitrix24.
//  НаКогоДелегировать - СправочникСсылка.ФизическиеЛица,
// Строка - Физическое лицо или идентификатор сотрудника в Bitrix24, которому делегируется задача.
//
// Возвращаемое значение:
//  Число - Код состояния HTTP-ответа.
//
&НаСервереБезКонтекста
Функция ДелегироватьЗадачу(ИДЗадачи, Знач НаКогоДелегировать) Экспорт
	
	ПараметрыСоединения = ПолучитьСоединениеЗаголовкиСBitrix24();
	Заголовки = ПараметрыСоединения.Заголовки;
	Соединение = ПараметрыСоединения.Соединение;
	
	АдресРесурса = "tasks.task.delegate";
	
	Если ТипЗнч(НаКогоДелегировать) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		ФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НаКогоДелегировать, "ФизическоеЛицо");
		ИДСотрудника = ПЛ_Битрикс24.ПолучитьIDBitrix24ПоФизЛицу(ФизЛицо);
		
	Иначе
		
		ИДСотрудника = НаКогоДелегировать;
		
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	ТестСтруктура = Новый Структура("taskId, userId", ИДЗадачи, ИДСотрудника);
	HTTPЗапрос.УстановитьТелоИзСтроки(ПЛ_МодульВебСервисов.ПреобразоватьВJSON(ТестСтруктура));
	
	Возврат Соединение.ОтправитьДляОбработки(HTTPЗапрос).КодСостояния;
	
КонецФункции

// Получает идентификатор постановщика задачи в Bitrix24 по идентификатору задачи.
//
// Параметры:
//  ИДЗадачи - Строка - Идентификатор задачи в Bitrix24.
//
// Возвращаемое значение:
//  HTTPОтвет - Результат выполнения HTTP-запроса к API Bitrix24.
//
&НаСервереБезКонтекста
Функция ПолучитьИДПостановщикаЗадачи(ИДЗадачи) Экспорт
	
	ПараметрыСоединения = ПолучитьСоединениеЗаголовкиСBitrix24();
	Заголовки = ПараметрыСоединения.Заголовки;
	Соединение = ПараметрыСоединения.Соединение;
	
	// https://apidocs.bitrix24.ru/api-reference/tasks/tasks-task-get.html
	АдресРесурса = "tasks.task.get";
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("CREATED_BY");
	
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	ТестСтруктура = Новый Структура("taskId, select", ИДЗадачи, МассивПолей);
	HTTPЗапрос.УстановитьТелоИзСтроки(ПЛ_МодульВебСервисов.ПреобразоватьВJSON(ТестСтруктура));
	
	Возврат Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	
КонецФункции

// Возвращает структуру со свойствами пароля пользователя.
//
// Возвращаемое значение:
//  Структура - содержит следующие ключи:
//   * НаименьшаяДлина - Число - минимальная длина пароля.
//   * Сложный - Булево - требуется ли сложный пароль.
//   * УчестьНастройки - Строка - настройки для пароля.
&НаСервереБезКонтекста
Функция СвойстваПароля()
	
	СвойстваПароля = Пользователи.СвойстваПароля();
	СвойстваПароля.НаименьшаяДлина = 8;
	СвойстваПароля.Сложный = Истина;
	СвойстваПароля.УчестьНастройки = "ДляПользователей";
	
	Возврат СвойстваПароля;
	
КонецФункции

// Процедура-обработчик события "ПриИзменении" табличной части "ТЗДанные".
//
// Параметры:
//  Элемент - ПолеФормы - элемент формы, связанный с табличной частью "ТЗДанные".
//
// Описание:
//  Процедура выполняет пересчет номеров строк в табличной части "ТЗДанные" после изменения данных.
//  Вызывает процедуру "ПересчитатьНомераСтрокТЗ" для выполнения пересчета.
&НаКлиенте
Процедура ТЗДанныеПриИзменении(Элемент)
	
	ПересчитатьНомераСтрокТЗ(Элемент);
	
КонецПроцедуры

// Процедура пересчитывает номера строк в таблице значений ТЗДанные.
//
// Параметры:
//  Элемент - <Неопределено или "<Тип элемента>" - Необязательный параметр, не используется в процедуре.
//
&НаКлиенте
Процедура ПересчитатьНомераСтрокТЗ(Элемент = Неопределено)
	
	Для каждого Стр Из ТЗДанные Цикл
		
		Стр.НомерСтроки = ТЗДанные.Индекс(Стр) + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура добавляет или обновляет пользователя 1С.
//
// Параметры:
//  СоответствиеПользователя1С - Соответствие - Соответствие с данными пользователя 1С,
// где ключ "Пользователь" содержит ссылку на пользователя,
//                                         а ключ "ФизЛицо" - ссылку на физическое лицо.
//  ЗадаватьПароль - Булево - Признак необходимости задания пароля пользователю.
//  СвойстваПароля - Структура - Свойства пароля пользователя.
//  ПользовательИБАутентификацияОС - Булево - Признак аутентификации пользователя по учетной записи ОС.
//  ПользовательИБПользовательОС - Строка - Имя пользователя ОС.
//  ПользовательИБАутентификацияСтандартная - Булево - Признак стандартной аутентификации пользователя.
//
&НаСервереБезКонтекста
Процедура ДобавитьОбновитьПользователя1С(
	СоответствиеПользователя1С,
	ЗадаватьПароль,
	СвойстваПароля,
	ПользовательИБАутентификацияОС = Ложь,
	ПользовательИБПользовательОС = "",
	ПользовательИБАутентификацияСтандартная = Ложь)
	
	// Инициализация основных переменных
	СтрПользователь = "Пользователь";
	
	// Каждый раз по новой определяем набор пораметров,
	// а иначе в ОписаниеПользователяИБ будет не пустой ПользовательИБ
	НовоеОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
	НовоеОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Истина;
	НовоеОписаниеПользователяИБ.ЗащитаОтОпасныхДействий = Ложь;
	НовоеОписаниеПользователяИБ.Вставить("Действие", "Записать");
	
	// Настраиваем параметры аутентификации
	НовоеОписаниеПользователяИБ.АутентификацияОС = ПользовательИБАутентификацияОС;
	НовоеОписаниеПользователяИБ.ПользовательОС = ПользовательИБПользовательОС;
	
	// Устанавливаем пароль, если требуется
	Если ЗадаватьПароль Тогда
		
		НовыйПароль = Пользователи.СоздатьПароль(СвойстваПароля);
		СоответствиеПользователя1С["ВременныйПароль"] = НовыйПароль;
		НовоеОписаниеПользователяИБ.Пароль = НовыйПароль;
		
	КонецЕсли;
	
	НовоеОписаниеПользователяИБ.АутентификацияСтандартная = ПользовательИБАутентификацияСтандартная;
	
	// Проверяем, нужно ли создавать нового пользователя
	Если Не ЗначениеЗаполнено(СоответствиеПользователя1С[СтрПользователь]) Тогда
		// Создаём нового пользователя
		
		// Форматируем имя пользователя
		ФамилияИнициалыФизЛица = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(СоответствиеПользователя1С["ФизЛицо"]);
		ФамилияИнициалыФизЛица = СтрЗаменить(ФамилияИнициалыФизЛица, ". ", ".");
		
		ПользовательДляОбработки = Справочники.Пользователи.СоздатьЭлемент();
		
		// Заполняем поля
		НовоеОписаниеПользователяИБ.Имя = ФамилияИнициалыФизЛица;
		НовоеОписаниеПользователяИБ.ПолноеИмя = ФамилияИнициалыФизЛица;
		ПользовательДляОбработки.Наименование = ФамилияИнициалыФизЛица;
		ПользовательДляОбработки.ФизическоеЛицо = СоответствиеПользователя1С["ФизЛицо"];
		//ПользовательДляОбработки.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", НовоеОписаниеПользователяИБ);
		
		//ПользовательДляОбработки.Записать();
		//
		//СоответствиеПользователя1С[СтрокаПользователь] = ПользовательДляОбработки.Ссылка;
		
	Иначе
		// Обновляем существующего пользователя
		
		// Форматируем имя
		СтрокаПользователь = Строка(СоответствиеПользователя1С[СтрПользователь]);
		
		ПользовательДляОбработки = СоответствиеПользователя1С[СтрПользователь].ПолучитьОбъект();
		
		// Обновляем поля
		ПользовательДляОбработки.Наименование = СтрокаПользователь;
		
		НовоеОписаниеПользователяИБ.Имя = СтрокаПользователь;
		НовоеОписаниеПользователяИБ.ПолноеИмя = СтрокаПользователь;
		
		ПользовательДляОбработки.Недействителен = Ложь;
		
		// ПользовательДляОбработки.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", НовоеОписаниеПользователяИБ);
		
		// ПользовательДляОбработки.Записать();
		
	КонецЕсли;
	
	// Добавляем описание пользователя и записываем
	ПользовательДляОбработки.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", НовоеОписаниеПользователяИБ);
	ПользовательДляОбработки.Записать();
	
	// Сохраняем ссылку на пользователя
	СоответствиеПользователя1С[СтрПользователь] = ПользовательДляОбработки.Ссылка;
	
КонецПроцедуры

// Добавляет или обновляет пользователя ТСД на сервере.
//
// Параметры:
//  СтрокаТЧ - СтрокаТаблицыЗначений - Строка табличной части, содержащая данные пользователя ТСД.
//
&НаСервереБезКонтекста
Процедура ДобавитьОбновитьПользователяТСД(СтрокаТЧ)
	
	ПараметрыПользователяТСД = ПараметрыОтправкиПользователея();
	
	ПарольТСД = ШтрихкодированиеПечатныхФорм.ЧисловойКодПоСсылке(СтрокаТЧ.Пользователь);
	
	МассивСкладов = Новый Массив;
	
	Для каждого СтрокаСЗ Из СтрокаТЧ.Склады Цикл
		
		МассивСкладов.Добавить(Строка(СтрокаСЗ.Значение.УникальныйИдентификатор()));
		
	КонецЦикла;
	
	ПараметрыПользователяТСД.id = Строка(СтрокаТЧ.Пользователь);
	ПараметрыПользователяТСД.name = Строка(СтрокаТЧ.Пользователь);
	ПараметрыПользователяТСД.password = ПарольТСД;
	ПараметрыПользователяТСД.barcode = ПарольТСД;
	ПараметрыПользователяТСД.groupId = СтрокаТЧ.ДоступВТСД;
	ПараметрыПользователяТСД.groupName = СтрокаТЧ.ДоступВТСД;
	ПараметрыПользователяТСД.warehouseIds = МассивСкладов;
	
	Результат = ДобавитьПользователяВТСД(ПараметрыПользователяТСД);
	
	Если НЕ Результат Тогда
		
		ШаблонОшибки = "Ошибка отправки. Пользователь ""%1"" не добавлен в ТСД!";
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонОшибки, СтрокаТЧ.Пользователь));
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет пользователя в ТСД.
//
// Параметры:
//  ПараметрыДобавления - Структура - Параметры для добавления пользователя в ТСД.
//
// Возвращаемое значение:
//  Булево - Результат добавления пользователя в ТСД.
//
&НаСервереБезКонтекста
Функция ДобавитьПользователяВТСД(ПараметрыДобавления)
	
	Результат = Ложь;
	
	АдресРесурса = "/Users";
	ТелоЗапроса = ПЛ_МодульВебСервисов.ПреобразоватьВJSON(ПараметрыДобавления, "ДобавитьПользователяВТСД");
	ПараметрыОтвета = УниверсальныеМеханизмыWS.ВыполнитьHTTPМетодMobileSmarts("POST", АдресРесурса, ТелоЗапроса);
	
	Если ПараметрыОтвета.КодСостояния = 200 ИЛИ ПараметрыОтвета.КодСостояния = 201 Тогда
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает начало выбора значения в колонке "Склады" таблицы ТЗДанные.
//
// Параметры:
//  Элемент - ПолеФормы - элемент формы, для которого обрабатывается событие.
//  ДанныеВыбора - СписокЗначений - список значений для выбора.
//  ВыборДобавлением - Булево - признак выбора добавлением.
//  СтандартнаяОбработка - Булево - признак стандартной обработки события.
//
// Описание:
//  Устанавливает тип значения для колонки "Склады" текущей строки таблицы ТЗДанные как СправочникСсылка.Склады,
//  если текущая строка таблицы не равна Неопределено.
//
&НаКлиенте
Процедура ТЗДанныеСкладыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТЗДанные.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("СправочникСсылка.Склады"));
		ТекущиеДанные.Склады.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
		
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает выбор физического лица в поле "ФизЛицоИсточник".
//
// Параметры:
//  Элемент - ПолеФормы - Поле формы, в котором был сделан выбор.
//  ВыбранноеЗначение - СправочникСсылка.ФизическиеЛица - Выбранное физическое лицо.
//  ДополнительныеДанные - Произвольный - Дополнительные данные выбора.
//  СтандартнаяОбработка - Булево - Признак стандартной обработки события.
//
&НаКлиенте
Процедура ФизЛицоИсточникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	ПользовательИсточник = ПользовательПоФизическомуЛицу(ВыбранноеЗначение);
	
	Если Не ЗначениеЗаполнено(ПользовательИсточник) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	АутентификацияПользователя = АутентификацияПользователяНаСервере(ПользовательИсточник);
	
	ПользовательИБАутентификацияСтандартнаяИсточник = АутентификацияПользователя.АутентификацияСтандартная;
	ПользовательИБАутентификацияОСИсточник = АутентификацияПользователя.АутентификацияОС;
	ПользовательИБПользовательОСИсточник = АутентификацияПользователя.ПользовательОС;
	
	ОчиститьЗаполнитьТЧ("ГруппыПользователейИсточника", ПользовательИсточник, "ГруппыПользователей");
	ОчиститьЗаполнитьТЧ("ГруппыДоступаИсточника", ПользовательИсточник, "ГруппыДоступа");
	
	ПотребоватьСменуПароляПриВходеИсточник = ПотребоватьСменуПароляПриВходе(ПользовательИсточник);
	
	ДоступСкладыТСД(ПользовательИсточник, ДоступВТСДИсточник, СкладыТСДИсточник);
	
	ОбновитьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура получает доступ к складам для пользователя ТСД.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого необходимо получить доступ к складам.
//  ДоступВТСД - Число - Группа доступа пользователя в ТСД.
//  СкладыТСД - Массив - Массив ссылок на склады, к которым пользователь имеет доступ в ТСД.
//
&НаСервере
Процедура ДоступСкладыТСД(Знач Пользователь, ДоступВТСД, СкладыТСД)
	
	
	ДоступВТСД = "";
	СкладыТСД.Очистить();
	
	ШаблонАдреса = "/Users(%1)";
	АдресРесурса = СтрШаблон(ШаблонАдреса, Строка(Пользователь));
	
	МетодЗапроса = "GET";
	
	ПараметрыОтвета = УниверсальныеМеханизмыWS.ВыполнитьHTTPМетодMobileSmarts(МетодЗапроса, АдресРесурса);
	
	Если ПараметрыОтвета.КодСостояния = 200 Или ПараметрыОтвета.КодСостояния = 204 Тогда
		
		СтруктураОтвета = ПЛ_МодульВебСервисов.ПреобразоватьИЗJSON(ПараметрыОтвета.ТелоСтрокой, , Истина);
		
		ДоступВТСД = СтруктураОтвета["groupId"];
		
		Для Каждого warehouseId Из СтруктураОтвета["warehouseIds"] Цикл
			
			СкладыТСД.Добавить(Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор(warehouseId)));
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Очищает и заполняет табличную часть объекта данными групп доступа пользователя.
//
// Параметры:
//  ИмяТЧ - Строка - Имя табличной части объекта.
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого необходимо получить группы доступа.
//  ТипГруппы - ПеречислениеСсылка.ТипыГруппДоступа - Тип групп доступа, которые необходимо получить.
//
&НаСервере
Процедура ОчиститьЗаполнитьТЧ(Знач ИмяТЧ, Знач Пользователь, Знач ТипГруппы)
	
	ЭтотОбъект[ИмяТЧ].Очистить();
	ЭтотОбъект[ИмяТЧ].Загрузить(ГруппыДоступаПользователя(Пользователь, ТипГруппы));
	
КонецПроцедуры

// Проверяет необходимость смены пароля при входе для указанного пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь,
// для которого необходимо проверить необходимость смены пароля.
//
// Возвращаемое значение:
//  Булево - Истина, если необходимо потребовать смену пароля при входе, иначе Ложь.
//
&НаСервереБезКонтекста
Функция ПотребоватьСменуПароляПриВходе(Знач Пользователь)
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияОПользователях.ПотребоватьСменуПароляПриВходе КАК ПотребоватьСменуПароляПриВходе
		|ИЗ
		|	РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|ГДЕ
		|	СведенияОПользователях.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.ПотребоватьСменуПароляПриВходе;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру с информацией об аутентификации пользователя.
//
// Параметры:
//  ПользовательИсточник - СправочникСсылка.Пользователи - Пользователь,
// для которого необходимо получить информацию об аутентификации.
//
// Возвращаемое значение:
//  Структура - Структура с ключами:
//   * АутентификацияСтандартная - Булево - Признак аутентификации пользователя стандартными средствами 1С.
//   * АутентификацияОС - Булево - Признак аутентификации пользователя средствами операционной системы.
//   * ПользовательОС - Строка - Имя пользователя операционной системы.
&НаСервереБезКонтекста
Функция АутентификацияПользователяНаСервере(Знач ПользовательИсточник)
	
	Результат = Новый Структура("АутентификацияСтандартная, АутентификацияОС, ПользовательОС");
	
	ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПользовательИсточник,
			"ИдентификаторПользователяИБ");
	СвойстваПользователяИБ = Пользователи.СвойстваПользователяИБ(ИдентификаторПользователяИБ);
	
	Если ЗначениеЗаполнено(СвойстваПользователяИБ) Тогда
		
		Результат.АутентификацияСтандартная = СвойстваПользователяИБ.АутентификацияСтандартная;
		Результат.АутентификацияОС = СвойстваПользователяИБ.АутентификацияОС;
		Результат.ПользовательОС = СвойстваПользователяИБ.ПользовательОС;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает событие изменения элемента "ПользовательИБАутентификацияОСИсточник".
//
// Параметры:
//  Элемент - ПолеФормы - элемент формы, для которого произошло событие изменения.
//
&НаКлиенте
Процедура ПользовательИБАутентификацияОСИсточникПриИзменении(Элемент)
	
	ОбновитьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура обновляет доступность элементов формы в зависимости от значений реквизитов.
//
// Параметры:
//  ПользовательИБАутентификацияОСИсточник - Булево - Признак аутентификации пользователя ИБ по ОС для источника.
//  ПользовательИБАутентификацияОСПриемник - Булево - Признак аутентификации пользователя ИБ по ОС для приемника.
//  ПолныеПрава - Булево - Признак наличия полных прав у текущего пользователя.
//
&НаКлиенте
Процедура ОбновитьДоступностьЭлементов()
	
	Элементы.ПользовательИБПользовательОСИсточник.Доступность = ПользовательИБАутентификацияОСИсточник;
	Элементы.ПользовательИБПользовательОСПриемник.Доступность = ПользовательИБАутентификацияОСПриемник;
	Элементы.ГруппыДоступаИсточника.Доступность = ПолныеПрава;
	Элементы.ГруппыДоступаПриемника.Доступность = ПолныеПрава;
	
КонецПроцедуры

// Возвращает таблицу значений с группами доступа пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого необходимо получить группы доступа.
//  ТипГруппы - Строка - Тип группы доступа, может принимать значения "ГруппыПользователей" или "ГруппыДоступа".
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица значений с колонкой "ГруппаПользователей"
// или "ГруппаДоступа" в зависимости от значения параметра ТипГруппы.
//
&НаСервереБезКонтекста
Функция ГруппыДоступаПользователя(Знач Пользователь, ТипГруппы)
	
	Если ТипГруппы = "ГруппыПользователей" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ГруппыПользователейСостав.Ссылка КАК ГруппаПользователей
			|ИЗ
			|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
			|ГДЕ
			|	ГруппыПользователейСостав.Пользователь = &Пользователь";
		
	ИначеЕсли ТипГруппы = "ГруппыДоступа" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ГруппыДоступаПользователи.Ссылка КАК ГруппаДоступа
			|ИЗ
			|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
			|ГДЕ
			|	ГруппыДоступаПользователи.Пользователь = &Пользователь";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаКлиенте
Процедура ТЗДанныеДоступВТСДПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТЗДанные.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено И НЕ ЗначениеЗаполнено(ТекДанные.Склады) Тогда
		ТекДанные.Склады = СкладыДляЗагрузки.Скопировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Если Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаСклад" Тогда
		ТЗДанные.Очистить();
	Иначе
		
		НомерЗадачиБитрикс = "";
		
		// Источник
		ФизЛицоИсточник = Неопределено;
		ПользовательИсточник = Неопределено;
		ПользовательИБАутентификацияСтандартнаяИсточник = Ложь;
		ПотребоватьСменуПароляПриВходеИсточник = Ложь;
		ПользовательИБАутентификацияОСИсточник = Ложь;
		ПользовательИБПользовательОСИсточник = Неопределено;
		ГруппыПользователейИсточника.Очистить();
		ГруппыДоступаИсточника.Очистить();
		ДоступВТСДИсточник = Неопределено;
		СкладыТСДИсточник = Неопределено;
		
		// Приемник
		ФизЛицоПриемник = Неопределено;
		ПользовательПриемник = Неопределено;
		ПользовательИБАутентификацияСтандартнаяПриемник = Ложь;
		ПотребоватьСменуПароляПриВходеПриемник = Ложь;
		ПользовательИБАутентификацияОСПриемник = Ложь;
		ПользовательИБПользовательОСПриемник = Неопределено;
		ГруппыПользователейПриемника.Очистить();
		ГруппыДоступаПриемника.Очистить();
		ДоступВТСДПриемник = Неопределено;
		СкладыТСДПриемник = Неопределено;
		ВременныйПарольПриемник = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьВсеНастройки(ПользовательИсточник, ПользовательПриемник)
	
	КопируемыеНастройки = Новый Массив;
	КопируемыеНастройки.Добавить("НастройкиОтчетов");
	КопируемыеНастройки.Добавить("НастройкиВнешнегоВида");
	КопируемыеНастройки.Добавить("ПерсональныеНастройки");
	КопируемыеНастройки.Добавить("Избранное");
	КопируемыеНастройки.Добавить("НастройкиПечати");
	КопируемыеНастройки.Добавить("ПрочиеПользовательскиеНастройки");
	
	Приемники = Новый Массив;
	Приемники.Добавить(ПользовательПриемник);
	
	Обработки.НастройкиПользователей.КопированиеНастроекПользователей(ПользовательИсточник, Приемники,
		КопируемыеНастройки);
	
КонецПроцедуры

#Область РаботаСПользователями

#КонецОбласти

#Область РаботаСБитриксом24

#КонецОбласти

#КонецОбласти
